/*
 * program4.ypp
 * Finley McIlwaine
 * Nov. 10, 2019
 * COSC4785, Program 4
 *
 * Bison grammar input file
*/
%{
#include <iostream>
#include "Error.hpp"
#include "Node.hpp"
#include "MyScanner.hpp"

using std::cout;
using std::endl;
using std::cerr;

extern Node *tree;
extern MyScanner scanner;
Error* err=new Error();

#define yylex() scanner.yylex()
#define YYERROR_VERBOSE 1

void yyerror(const char *);
%}

%locations

  /*
   * El Union
   */
%union {
  Node *pnode;
}

/*
 * token stuff
 */
%type<pnode> classdec
%type<pnode> classbody
%type<pnode> methoddecs
%type<pnode> constructordecs
%type<pnode> vardecs
%type<pnode> vardec
%type<pnode> constructordec
%type<pnode> methoddec
%type<pnode> parameterlist
%type<pnode> parameter
%type<pnode> block
%type<pnode> localvardecs
%type<pnode> stmts
%type<pnode> localvardec
%type<pnode> stmt
%type<pnode> conditionalstmt
%type<pnode> optionalexp
%type<pnode> name
%type<pnode> program
%type<pnode> simpletype
%type<pnode> type
%type<pnode> arglist
%type<pnode> newexp
%type<pnode> exp
%type<pnode> multibrackets
%type<pnode> bracketexp
%type<pnode> bracketexps
%type<pnode> idbrack

%token<pnode> VOID
%token<pnode> CLASS
%token<pnode> RETURN
%token<pnode> WHILE
%token<pnode> PRINT
%token<pnode> SEMI
%token<pnode> ELSE
%token<pnode> IF
%token<pnode> ASSIGN
%token<pnode> READ
%token<pnode> NULLT
%token<pnode> DOT
%token<pnode> THIS
%token<pnode> INT
%token<pnode> NEW
%token<pnode> COMMA
%token<pnode> NUMBER
%token<pnode> ID
%token<pnode> RBRACK LPAREN RPAREN LBRACE RBRACE
%left<pnode> EQ NE LE GE LT GT
%left<pnode> PLUS MINUS OR
%left<pnode> TIMES DIV MOD AND
%token<pnode> NOT

%precedence UNARYOP
%precedence IF
%precedence ELSE
%precedence<pnode> LBRACK
/*
 * The Grammar :o
 */
%%
input: program {
  tree=new Node($1);
}

;

program: program classdec {
  $$=new ProgramNode($1,$2);
  $$->setVal("<Program> <ClassDec>");
}
| classdec {
  $$=new ProgramNode($1);
  $$->setVal("<ClassDec>");
}
| error {
  err->withColNumber(@1.first_column)->withLineNumber(@1.first_line);
  scanner.addError(*err);
  $$=new ProgramNode();
  $$->setVal("<Error>");
}

; /* do not ever even think about forgetting this stupid thing */ 

classdec: CLASS ID classbody {
  $$=new ClassDecNode($2,$3);
  $$->setVal("class identifier <ClassBody>");
}
| error ID classbody {
  err->withColNumber(@1.first_column)->withLineNumber(@1.first_line);
  scanner.addError(*err);
  $$=new ClassDecNode();
  $$->setVal("<Error> identifier <ClassBody>");
}
| CLASS error classbody {
  err->withColNumber(@2.first_column)->withLineNumber(@2.first_line);
  scanner.addError(*err);
  $$=new ClassDecNode();
  $$->setVal("class <Error> <ClassBody>");
}
| CLASS ID error {
  err->withColNumber(@3.first_column)->withLineNumber(@3.first_line);
  scanner.addError(*err);
  $$=new ClassDecNode();
  $$->setVal("class identifier <Error>");
}

;

classbody: LBRACE vardecs constructordecs methoddecs RBRACE {
  $$=new ClassBodyNode($2,$3,$4);
  $$->setVal("lbrace <VarDecs> <ConstructorDecs> <MethodDecs> rbrace");
}
| LBRACE vardecs constructordecs RBRACE {
  $$=new ClassBodyNode($2,$3);
  $$->setVal("lbrace <VarDecs> <ConstructorDecs> rbrace");
}
| LBRACE vardecs methoddecs RBRACE {
  $$=new ClassBodyNode($2,$3);
  $$->setVal("lbrace <VarDecs> <MethodDecs> rbrace");
}
| LBRACE vardecs RBRACE {
  $$=new ClassBodyNode($2);
  $$->setVal("lbrace <VarDecs> rbrace");
}
| LBRACE constructordecs methoddecs RBRACE {
  $$=new ClassBodyNode($2,$3);
  $$->setVal("lbrace <ConstructorDecs> <MethodDecs> rbrace");
}
| LBRACE constructordecs RBRACE {
  $$=new ClassBodyNode($2);
  $$->setVal("lbrace <ConstructorDecs> rbrace");
}
| LBRACE methoddecs RBRACE {
  $$=new ClassBodyNode($2);
  $$->setVal("lbrace <MethodDecs> rbrace");
}
| LBRACE RBRACE {
  $$=new ClassBodyNode();
  $$->setVal("lbrace rbrace");
}

;

vardecs: vardecs vardec {
  $$=new VarDecsNode($1,$2);
  $$->setVal("<VarDecs> <VarDec>");
}
| vardec {
  $$=new VarDecsNode($1);
  $$->setVal("<VarDec>");
}
;

vardec: type ID SEMI {
  $$=new VarDecNode($1,$2);
  $$->setVal("<Type> identifier semi");
}
| idbrack ID SEMI {
  $$=new VarDecNode($1,$2);
  $$->setVal("<IdBrack> identifier semi");
}

;

type: simpletype {
  $$=new TypeNode($1);
  $$->setVal("<SimpleType>");
}
| type LBRACK RBRACK {
  $$=new TypeNode($1);
  $$->setVal("<Type> lbrack rbrack");
}

;

simpletype: INT {
  $$=new SimpleTypeNode();
  $$->setVal("int");
}

;

constructordecs: constructordecs constructordec {
  $$=new ConstructorDecsNode($1,$2);
  $$->setVal("<ConstructorDecs> <ConstructorDec>");
}
| constructordec {
  $$=new ConstructorDecsNode($1);
  $$->setVal("<ConstructorDec>");
}
;

constructordec: ID LPAREN parameterlist RPAREN block {
  $$=new ConstructorDecNode($1,$3,$5);
  $$->setVal("identifier lparen <ParameterList> rparen <Block>");
}

;

methoddecs: methoddecs methoddec {
  $$=new MethodDecsNode($1,$2);
  $$->setVal("<MethodDecs> <MethodDec>");
}
| methoddec {
  $$=new MethodDecsNode($1);
  $$->setVal("<MethodDec>");
}

;

methoddec: type ID LPAREN parameterlist RPAREN block {
  $$=new MethodDecNode($1,$2,$4,$6);
  $$->setVal("<Type> identifier lparen <ParameterList> rparen <Block>");
}
| VOID ID LPAREN parameterlist RPAREN block {
  $$=new MethodDecNode($2,$4,$6);
  $$->setVal("VOID identifier lparen <ParameterList> rparen <Block>");
}
| idbrack ID LPAREN parameterlist RPAREN block {
  $$=new MethodDecNode($1,$2,$4,$6);
  $$->setVal("<IdBrack> identifier lparen <ParameterList> rparen <Block>");
}

;

parameterlist: parameter {
  $$=new ParameterListNode($1);
  $$->setVal("<Parameter>");
}
| parameterlist COMMA parameter {
  $$=new ParameterListNode($1,$3);
  $$->setVal("<ParameterList> comma <Exp>");
}
| %empty {
  $$=new ParameterListNode();
  $$->setVal("empty"); /* HOW TO SHOW THIS??? */
}

;

parameter: type ID {
  $$=new ParameterNode($1,$2);
  $$->setVal("<Type> identifier");
}
| idbrack ID {
  $$=new ParameterNode($1,$2);
  $$->setVal("<IdBrack> identifier");
}

;

block: LBRACE localvardecs stmts RBRACE {
  $$=new BlockNode($2,$3);
  $$->setVal("lbrace <LocalVarDecs> <Stmts> rbrace");
}
| LBRACE localvardecs RBRACE {
  $$=new BlockNode($2);
  $$->setVal("lbrace <LocalVarDecs> rbrace");
}
| LBRACE stmts RBRACE {
  $$=new BlockNode($2);
  $$->setVal("lbrace <Stmts> rbrace");
}
| LBRACE RBRACE {
  $$=new BlockNode();
  $$->setVal("lbrace rbrace");
}

;

localvardecs: localvardecs localvardec {
  $$=new LocalVarDecsNode($1,$2);
  $$->setVal("<LocalVarDecs> <LocalVarDec>");
}
| localvardec {
  $$=new LocalVarDecsNode($1);
  $$->setVal("<LocalVarDec>");
}

;

localvardec: type ID SEMI {
  $$=new LocalVarDecNode($1,$2);
  $$->setVal("<Type> identifier semi");
}
| idbrack ID SEMI {
  $$=new LocalVarDecNode($1,$2);
  $$->setVal("<IdBrack> identifier semi");
}

;

stmts: stmts stmt {
  $$=new StmtsNode($1,$2);
  $$->setVal("<Stmts> <Stmt>");
}
| stmt {
  $$=new StmtsNode($1);
  $$->setVal("<Stmt>");
}

;

stmt: SEMI {
  $$=new StmtNode();
  $$->setVal("semi");
}
| name ASSIGN exp SEMI {
  $$=new StmtNode($1,$3);
  $$->setVal("<Name> assign <Exp> semi");
}
| name LPAREN arglist RPAREN SEMI {
  $$=new StmtNode($1,$3);
  $$->setVal("<Name> lparen <ArgList> rparen semi");
}
| PRINT LPAREN arglist RPAREN SEMI {
  $$=new StmtNode($3);
  $$->setVal("print lparen <ArgList> rparen semi");
}
| conditionalstmt {
  $$=new StmtNode($1);
  $$->setVal("<ConditionalStmt>");
}
| WHILE LPAREN exp RPAREN stmt {
  $$=new StmtNode($3,$5);
  $$->setVal("while lparen <Exp> rparen <Stmt>");
}
| RETURN optionalexp SEMI {
  $$=new StmtNode($2);
  $$->setVal("return <OptionalExp> semi");
}
| block {
  $$=new StmtNode($1);
  $$->setVal("<Block>");
}

;

name: THIS {
  $$=new NameNode();
  $$->setVal("this");
}
| ID %prec IF {
  $$=new NameNode($1);
  $$->setVal("identifier");
}
| name DOT ID {
  $$=new NameNode($1,$3);
  $$->setVal("<Name> dot identifier");
}
| name bracketexp {
  $$=new NameNode($1,$2);
  $$->setVal("<Name> <BracketExp>");
}

;

arglist: exp {
  $$=new ArgListNode($1);
  $$->setVal("<Exp>");
}
| arglist COMMA exp {
  $$=new ArgListNode($1,$3);
  $$->setVal("<ArgList> comma <Exp>");
}
| %empty {
  $$=new ArgListNode();
  $$->setVal("empty"); /* HOW TO SHOW THIS??? */
}

;

conditionalstmt: IF LPAREN exp RPAREN stmt %prec IF {
  $$=new ConditionalStmtNode($3,$5);
  $$->setVal("if lparen <Exp> rparen <Stmt>");
}
| IF LPAREN exp RPAREN stmt ELSE stmt %prec ELSE {
  $$=new ConditionalStmtNode($3,$5,$7);
  $$->setVal("if lparen <Exp> rparen <Stmt> else <Stmt>");
}

;

optionalexp: exp {
  $$=new OptionalExpNode($1);
  $$->setVal("<Exp>");
}
| %empty {
  $$=new OptionalExpNode();
  $$->setVal("empty");
}

;

exp: name {
  $$=new ExpNode($1);
  $$->setVal("<Name>");
}
| NUMBER {
  $$=new ExpNode($1);
  $$->setVal("number");
}
| NULLT {
  $$=new ExpNode();
  $$->setVal("null");
}
| name LPAREN arglist RPAREN {
  $$=new ExpNode($1,$3);
  $$->setVal("<Name> lparen <ArgList> rparen");
}
| READ LPAREN RPAREN {
  $$=new ExpNode();
  $$->setVal("read lparen rparen");
}
| newexp {
  $$=new ExpNode($1);
  $$->setVal("<NewExp>");
}
| PLUS exp %prec UNARYOP {
  $$=new ExpNode($2);
  $$->setVal("plus <Exp>");
}
| MINUS exp %prec UNARYOP {
  $$=new ExpNode($2);
  $$->setVal("minus <Exp>");
}
| NOT exp %prec UNARYOP {
  $$=new ExpNode($2);
  $$->setVal("not <Exp>");
}
| NOT error {
  err->withColNumber(@2.first_column)->withLineNumber(@2.first_line);
  scanner.addError(*err);
  $$=new ExpNode();
  $$->setVal("not <Error>");
}
| exp EQ exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> eq <Exp>");
}
| exp NE exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> ne <Exp>");
}
| exp LE exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> le <Exp>");
}
| exp GE exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> ge <Exp>");
}
| exp LT exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> lt <Exp>");
}
| exp GT exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> gt <Exp>");
}
| exp PLUS exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> plus <Exp>");
}
| exp MINUS exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> minus <Exp>");
}
| exp OR exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> or <Exp>");
}
| exp TIMES exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> times <Exp>");
}
| exp DIV exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> div <Exp>");
}
| exp MOD exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> mod <Exp>");
}
| exp AND exp {
  $$=new ExpNode($1,$3);
  $$->setVal("<Exp> and <Exp>");
}
| LPAREN exp RPAREN {
  $$=new ExpNode($2);
  $$->setVal("lparen <Exp> rparen");
}

;

newexp: NEW ID LPAREN arglist RPAREN {
  $$=new NewExpNode($2,$4);
  $$->setVal("new identifier lparen <ArgList> rparen");
}
| NEW simpletype bracketexps multibrackets {
  $$=new NewExpNode($2,$3,$4);
  $$->setVal("new <SimpleType> <BracketExps> <MultiBrackets>");
}
| NEW ID bracketexps multibrackets {
  $$=new NewExpNode($2,$3,$4);
  $$->setVal("new identifier <BracketExps> <MultiBrackets>");
}
| NEW simpletype bracketexps {
  $$=new NewExpNode($2,$3);
  $$->setVal("new <SimpleType> <BracketExps>");
}
| NEW ID bracketexps {
  $$=new NewExpNode($2,$3);
  $$->setVal("new identifier <BracketExps>");
}

;

bracketexps: bracketexp {
  $$=new BracketExpsNode($1);
  $$->setVal("<BracketExp>");
}
| bracketexps bracketexp {
  $$=new BracketExpsNode($1,$2);
  $$->setVal("<BracketExps> <BracketExp>");
}

;

bracketexp: LBRACK exp RBRACK {
  $$=new BracketExpNode($2);
  $$->setVal("lbrack <Exp> rbrack");
}

;

multibrackets: LBRACK RBRACK {
  $$=new MultibracketNode();
  $$->setVal("lbrack rbrack");
}
| multibrackets LBRACK RBRACK {
  $$=new MultibracketNode($1);
  $$->setVal("<Multibrackets> lbrack rbrack");
}

idbrack: ID {
  $$=new IdBrackNode($1);
  $$->setVal("identifier");
}
| ID multibrackets {
  $$=new IdBrackNode($1,$2);
  $$->setVal("identifier <MultiBrackets>");
}
;


%%
void yyerror(char const *s) 
{
  // Shut up Bison let me do the talking (please and thank you)
  err->withDesc(s);
} 
